buildscript {
  repositories {
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath 'org.gebish:geb-gradle:2.2'
    classpath 'commons-io:commons-io:2.6'
    classpath 'gradle.plugin.com.energizedwork.webdriver-binaries:webdriver-binaries-gradle-plugin:1.4'
  }
}

apply plugin: "idea"
apply plugin: "groovy"
apply plugin: "geb-browserstack"
apply plugin: "com.energizedwork.webdriver-binaries"

ext {
  chromeDriverVersion = '2.42'
  geckoDriverVersion = '0.23.0'
  gebVersion = '2.2'
  jacksonVersion = '2.9.6'
  seleniumVersion = '3.14.0'
  slf4jVersion = '1.7.13'
  spockVersion = '1.1-groovy-2.4'
  spockReportVersion = '1.6.0'
  unirestVersion = '1.4.9'
  groovyVersion = '2.4.12'

  drivers = ["firefox", "chrome", "chromeHeadless"]
}

dependencies {
  testCompile("com.athaydes:spock-reports:$spockReportVersion") {
    transitive = false
  }
  testCompile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
  testCompile "com.mashape.unirest:unirest-java:$unirestVersion"
  testCompile "org.gebish:geb-spock:$gebVersion"
  testCompile "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
  testCompile "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
  testCompile "org.spockframework:spock-core:$spockVersion"
  testCompile "org.slf4j:slf4j-api:$slf4jVersion"
  testCompile "org.slf4j:slf4j-simple:$slf4jVersion"
}

repositories {
  jcenter()
  mavenCentral()
}

webdriverBinaries {
  chromedriver chromeDriverVersion
  geckodriver geckoDriverVersion
}

browserStack {
  task {
    systemProperty 'geb.build.reportsDir', reporting.file("$project.buildDir/test-results/$name/geb")
    systemProperty 'com.athaydes.spockframework.report.outputDir', reporting.file("$project.buildDir/reports/docs/$name")
  }
  browsers {
    create('edge_windows_17') {
      capabilities os: 'WINDOWS', os_version: "10"
    }
    create('firefox_windows_62') {
      capabilities os: 'WINDOWS', os_version: "10"
    }
    create('chrome_windows_69') {
      capabilities os: 'WINDOWS', os_version: "10"
    }
    create('internet explorer_windows_11') {
      capabilities os: 'WINDOWS', os_version: "10"
    }
    create('firefox_mac_62') {
      capabilities os: 'OS X', os_version: "High Sierra"
    }
    create('chrome_mac_69') {
      capabilities os: 'OS X', os_version: "High Sierra"
    }
    create('safari_mac_11.1') {
      capabilities os: 'OS X', os_version: "High Sierra"
    }
  }
  account {
    username = System.getenv('BROWSERSTACK_USERNAME')
    accessKey = System.getenv('BROWSERSTACK_ACCESS_KEY')
  }
}

drivers.each { driver ->
  task "${driver}Test"(type: Test) {
    group 'Geb Local Tests'

    outputs.upToDateWhen { false }  // Always run tests

    systemProperty "geb.build.reportsDir", reporting.file("$project.buildDir/test-results/$name/geb")
    systemProperty 'com.athaydes.spockframework.report.outputDir', reporting.file("$project.buildDir/reports/docs/$name")
    systemProperty "geb.env", driver
  }
}