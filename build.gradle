import org.apache.tools.ant.taskdefs.condition.Os
import org.apache.commons.io.FileUtils

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.gebish:geb-gradle:2.2'
        classpath 'commons-io:commons-io:2.6'
    }
}

apply plugin: "idea"
apply plugin: "groovy"
apply plugin: "geb-browserstack"

ext {
    gebVersion = '2.2'
    spockVersion = '1.1-groovy-2.4'
    seleniumVersion = '3.14.0'
    chromeDriverVersion = '2.41'
}

dependencies {
    testCompile "org.gebish:geb-spock:$gebVersion"
    testCompile "org.spockframework:spock-core:$spockVersion"
    testCompile "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
}

repositories {
    jcenter()
}

task downloadChromeDriver {
    def outputFile = file("$buildDir/webdriver/chromedriver.zip")
    inputs.property("chromeDriverVersion", chromeDriverVersion)
    outputs.file(outputFile)

    doLast {
        def driverOsFilenamePart
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            driverOsFilenamePart = "win32"
        } else if (Os.isFamily(Os.FAMILY_MAC)) {
            driverOsFilenamePart = "mac64"
        } else if (Os.isFamily(Os.FAMILY_UNIX)) {
            driverOsFilenamePart = Os.isArch("amd64") ? "linux64" : "linux32"
        }
        FileUtils.copyURLToFile(new URL("http://chromedriver.storage.googleapis.com/${chromeDriverVersion}/chromedriver_${driverOsFilenamePart}.zip"), outputFile)
    }
}

task unzipChromeDriver(type: Copy) {
    def outputDir = file("$buildDir/webdriver/chromedriver")
    dependsOn downloadChromeDriver
    outputs.dir(outputDir)

    from(zipTree(downloadChromeDriver.outputs.files.singleFile))
    into(outputDir)
}

test {
    include "**/*Spec*"

    reports {
        html.destination = reporting.file("$name/tests")
        junitXml.destination = file("$buildDir/test-results/$name")
    }

    systemProperty "geb.build.reportsDir", reporting.file("$name/geb")
    systemProperty "geb.env", "chrome"

    dependsOn unzipChromeDriver

    def chromedriverFilename = Os.isFamily(Os.FAMILY_WINDOWS) ? "chromedriver.exe" : "chromedriver"
    systemProperty "webdriver.chrome.driver", new File(unzipChromeDriver.outputs.files.singleFile, chromedriverFilename).absolutePath

    systemProperty "geb.build.baseUrl", "https://stg.pro.skilling.com"
    systemProperty "geb.build.reportsDir", reporting.file("$project.buildDir/test-results/$name/geb")
}

browserStack {
    application "https://www.google.com"
    task {
        testClassesDir = rootProject.test.testClassesDir
        testSrcDirs = rootProject.test.testSrcDirs
        classpath = rootProject.test.classpath

        systemProperty "geb.build.reportsDir", reporting.file("$project.buildDir/test-results/$name/geb")
    }
    account {
        username = System.getenv('BROWSERSTACK_USERNAME')
        accessKey = System.getenv('BROWSERSTACK_ACCESS_KEY')
    }
}