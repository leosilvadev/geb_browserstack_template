import org.apache.tools.ant.taskdefs.condition.Os
import org.apache.commons.io.FileUtils

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.gebish:geb-gradle:2.2'
        classpath 'commons-io:commons-io:2.6'
    }
}

apply plugin: "idea"
apply plugin: "groovy"
apply plugin: "geb-browserstack"

ext {
    gebVersion = '2.2'
    spockVersion = '1.1-groovy-2.4'
    seleniumVersion = '3.14.0'
    chromeDriverVersion = '2.41'
}

dependencies {
    testCompile "org.gebish:geb-spock:$gebVersion"
    testCompile "org.spockframework:spock-core:$spockVersion"
    testCompile "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
}

repositories {
    jcenter()
}

browserStack {
    application "https://www.google.com"
    task {
        systemProperty "geb.build.reportsDir", reporting.file("$project.buildDir/test-results/$name/geb")
    }
    browsers {
        firefox {
            capabilities os: "WINDOWS", os_version: "8.1"
        }
        edge {
            capabilities os: "WINDOWS", os_version: "10"
        }
        safari {
            capabilities os: "OS X", os_version: "High Sierra"
        }
        create("internet explorer") {
            capabilities os: "WINDOWS", os_version: "7", browser_version: "11"
        }
    }
    account {
        username = System.getenv('BROWSERSTACK_USERNAME')
        accessKey = System.getenv('BROWSERSTACK_ACCESS_KEY')
    }
}